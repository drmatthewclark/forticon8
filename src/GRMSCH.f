C
C     FORTICON8  JANUARY 2018
C     MATTHEW CLARK
C
      SUBROUTINE GRMSCH(U,C,NDIM)
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION U(NDIM,NDIM),C(NDIM)
C
C    SUBROUTINE TO CARRY OUT A GRAM SCHMIDT ORTHOGONALIZATION ON A
C    SET OF VECTORS X(I). THEY ARE CONVERTED INTO A SET OF ORTHONORMAL
C    VECTORS Y(J).  THE UNITARY TRANSFORMATION IS DEVELOPED.
C
C    U   CONTAINS THE OVERLAP MATRIX IN THE UPPER RIGHT TRIANGULAR
C        PART. THE LOWER TRIANGULAR PART IS NOT USED. THE UNITARY
C        TRANSFORMATION IS RETURNED IN THE UPPER RIGHT TRIANGLE,
C        INCLUDING THE DIAGONAL.
C    C   IS A WORK VECTOR OF LENGTH NDIM.
C
C    NDIM IS THE DIMENSION OF C AND U (IE. C(NDIM), U(NDIM,NDIM)).
C
C    ACTUALLY THE FIRST COLUMN OF U MAY BE USED AS THE WORK AREA
C    PROVIDED THAT THE ELEMENT U(1,1) IS SET EQUAL TO 1.D0 AFTER
C    RETURN IS MADE TO THE CALLING PROGRAM.
C
      U(1,1)=1.D0
      DO 100 I=2,NDIM
      I1=I-1
      XNORM=1.D0
      DO 40 J=1,I1
      SUM=0.D0
      DO 30 K=1,J
      SUM=SUM+U(K,J)*U(K,I)
 30   CONTINUE
      C(J)=SUM
      XNORM=XNORM-SUM**2
 40   CONTINUE
      XNORM=DSQRT(DABS(1.D0/XNORM))
      DO 70 J=1,I1
      SUM=0.D0
      DO 60 K=J,I1
      SUM=SUM+C(K)*U(J,K)
 60   CONTINUE
      U(J,I)=-SUM*XNORM
 70   CONTINUE
      U(I,I)=XNORM
 100  CONTINUE
      RETURN
      END
